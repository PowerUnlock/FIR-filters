`timescale 1ns/1ps

module tb_fir_genvar;

  parameter integer N_TAPS   = 100;
  parameter integer NSAMPLES = 2000;
  parameter integer TCLK     = 10;   // ns => 100 MHz

  reg clk;
  reg rst;
  reg vin;
  reg signed [15:0] xin;

  wire vout;
  wire signed [15:0] yout;

  reg [15:0] xmem [0:NSAMPLES-1];

  integer n;
  integer fout;

  // DUT

  fir_genvar #(.N(N_TAPS)) dut (
    .clk (clk),
    .rst (rst),
    .vin (vin),
    .xin (xin),
    .vout(vout),
    .yout(yout)
  );

  // Clock

  initial begin
    clk = 1'b0;
    forever #(TCLK/2) clk = ~clk;
  end

  // synchronous reset

  task apply_reset;
    begin
      rst = 1'b1;
      vin = 1'b0;
      xin = 16'sd0;

     
      @(posedge clk);
      @(posedge clk);
      @(posedge clk);

      rst = 1'b0;

      
      @(posedge clk);
    end
  endtask

  // Log output whenever valid

  always @(negedge clk) begin
    if (!rst && vout) begin
      $fwrite(fout, "%04h\n", yout[15:0]);
    end
  end

//...................................................

integer pos_cnt = 0;

always @(posedge clk) begin
  pos_cnt = pos_cnt + 1;

  if (pos_cnt >= 1 && pos_cnt <= 8) begin
    // Immediate at posedge (active region)
    $display("posedge=%0d t=%0t  rst=%b vin=%b xin=0x%04h  vin_d=%b  vout=%b yout=0x%04h",
             pos_cnt, $time,
             rst, vin, xin[15:0],
             dut.vin_d, vout, yout[15:0]);

    // 1 ns after posedge (after NBAs + your stimulus updates)
    #1;
    $display("     +1ns t=%0t  rst=%b vin=%b xin=0x%04h  vin_d=%b  vout=%b yout=0x%04h",
             $time,
             rst, vin, xin[15:0],
             dut.vin_d, vout, yout[15:0]);
  end
end

//.................................................



//.................................................

  // Run file:

  task run_file;
    input [8*64-1:0] in_filename;
    input [8*64-1:0] out_filename;
    begin

    
      $readmemh(in_filename, xmem);   // Load inputs

     
      fout = $fopen(out_filename, "w");  // Open output file
      if (fout == 0) begin
        $display("ERROR: could not open output file %s", out_filename);
        $finish;
      end

    
      apply_reset;   // Reset DUT

      // Feed 1 sample per clock.
     
      vin = 1'b1;
      for (n = 0; n < NSAMPLES; n = n + 1) begin
        xin = $signed(xmem[n]);   
        @(posedge clk);           
      end

      // Stop feeding 

      xin = 16'sd0;
      vin = 1'b0;
      @(posedge clk);

      
      for (n = 0; n < 10; n = n + 1) begin // flush 10 cycles for safety
        @(posedge clk);
      end

      $fclose(fout);
    end
  endtask

  

  // Main
  initial begin
    rst = 1'b1;
    vin = 1'b0;
    xin = 16'sd0;

    run_file("x950_q214_hex.txt",  "y950_genvar_hex.txt");
    run_file("x1100_q214_hex.txt", "y1100_genvar_hex.txt");
    run_file("x2000_q214_hex.txt", "y2000_genvar_hex.txt");

    $display("DONE: genvar FIR outputs written.");
    $finish;
  end

endmodule
